// Top-level build file where you can add configuration options common to all sub-projects/modules.
//buildscript {
//    repositories {
//        mavenCentral()
//        maven { url 'https://developer.huawei.com/repo/' }
//    }
//    dependencies {
//        classpath 'com.android.tools.build:gradle:8.8.0'
//        classpath 'com.huawei.agconnect:agcp:1.9.3.300'
//    }
//}

plugins {
    alias(libs.plugins.android.application) apply false
    alias(libs.plugins.google.services) apply false
    alias(libs.plugins.secrets.gradle.plugin) apply false
    alias(libs.plugins.kotlin.android) apply false
}

subprojects {
    afterEvaluate {
        if (plugins.hasPlugin("com.android.library") ||
                plugins.hasPlugin("com.android.application")) {
            android {
                compileSdk = 35
                buildToolsVersion = "35.0.1"
                ndkVersion = "27.2.12479018"

                lint {
                    checkReleaseBuilds = false
                    disable = [
                            'MissingTranslation',
                            'ExtraTranslation',
                            'BlockedPrivateApi'
                    ]
                }

                compileOptions {
                    sourceCompatibility = JavaVersion.VERSION_21
                    targetCompatibility = JavaVersion.VERSION_21

                    coreLibraryDesugaringEnabled = true
                }

                defaultConfig {
                    minSdk = 21
                    //noinspection OldTargetApi
                    targetSdk = 34
                }

                buildTypes {
                    play {
                        initWith(buildTypes.release)
                    }
                }

                buildFeatures.buildConfig = true
            }

            dependencies {
                coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs_nio:2.1.5'
            }
        }
    }
}

tasks.register('clean', Delete) {
    delete layout.buildDirectory.get()
}

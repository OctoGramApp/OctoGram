import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform

import java.nio.file.Paths

plugins {
    id 'com.android.library'
}

configurations.configureEach {
    exclude group: 'com.google.firebase', module: 'firebase-core'
    exclude group: 'androidx.recyclerview', module: 'recyclerview'
}

def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim().substring(0, 8)
}

def getGitDate = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'show', '-s', '--format=%ct'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

def getGitBranch = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--abbrev-ref', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().replace("feature/", "").trim()
}

def getVersionPBetaName = { ->
    def version_name = APP_VERSION_NAME.split(" ")[0]
    return version_name + " #" + getGitBranch()
}

def isWindows = DefaultNativePlatform.currentOperatingSystem.toFamilyName() == OperatingSystemFamily.WINDOWS
def isMacOs = DefaultNativePlatform.currentOperatingSystem.toFamilyName() == OperatingSystemFamily.MACOS

def getCCacheExecutable = {
    def pathName
    if (isWindows) {
        pathName = "windows"
    } else if (isMacOs) {
        // Check if ccache is installed
        if (new ProcessBuilder("which", "ccache").start().waitFor() == 0) {
            def stdout = new ByteArrayOutputStream()
            exec {
                commandLine 'which', 'ccache'
                standardOutput = stdout
            }
            pathName = stdout.toString().trim()
            // Get the path to the ccache executable
            return Paths.get(pathName).toFile()
        }
        pathName = "macOs"
    } else {
        if (new ProcessBuilder("which", "ccache").start().waitFor() == 0) {
            def stdout = new ByteArrayOutputStream()
            exec {
                commandLine 'which', 'ccache'
                standardOutput = stdout
            }
            pathName = stdout.toString().trim()
            // Get the path to the ccache executable
            return Paths.get(pathName).toFile()
        }
        pathName = "linux"
    }
    def executableFile = "ccache"
    if (isWindows) {
        executableFile += ".exe"
    }
    // Build the path to the local ccache executable
    return Paths.get(System.getProperty("user.dir"), "Tools", "ccache", pathName, executableFile).toFile()
}

dependencies {
    // AndroidX dependencies
    implementation 'androidx.fragment:fragment:1.6.2'
    implementation 'androidx.core:core:1.10.1' // REQUIRES MIGRATING GRADLE TO A MORE UP-TO-DATE VERSION
    implementation 'androidx.palette:palette:1.0.0'
    implementation 'androidx.exifinterface:exifinterface:1.3.7'
    implementation 'androidx.dynamicanimation:dynamicanimation:1.0.0'
    implementation 'androidx.sharetarget:sharetarget:1.2.0'
    implementation 'androidx.cardview:cardview:1.0.0'

    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'
    //noinspection GradleDependency
    compileOnly 'org.checkerframework:checker-qual:3.36.0' // REQUIRES MIGRATING GRADLE TO A MORE UP-TO-DATE VERSION
    compileOnly 'org.checkerframework:checker-compat-qual:2.5.5'

    // Firebase dependencies
    implementation 'com.google.firebase:firebase-messaging:23.4.1'
    implementation 'com.google.firebase:firebase-config:21.6.3'
    implementation 'com.google.firebase:firebase-datatransport:18.2.1'
    implementation 'com.google.firebase:firebase-appindexing:20.0.0'

    // Google Play Services dependencies
    implementation 'com.google.android.gms:play-services-maps:18.2.0'
    implementation 'com.google.android.gms:play-services-auth:21.0.0'
    implementation 'com.google.android.gms:play-services-vision:20.1.3'
    implementation 'com.google.android.gms:play-services-wearable:18.1.0'
    //noinspection GradleDependency
    implementation 'com.google.android.gms:play-services-location:21.0.1' // NOT YET SUPPORTED BY TELEGRAM
    implementation 'com.google.android.gms:play-services-wallet:19.3.0'
    implementation 'com.google.android.gms:play-services-safetynet:18.0.1'

    //noinspection GradleDependency
    implementation 'com.googlecode.mp4parser:isoparser:1.0.6' // NOT YET SUPPORTED BY TELEGRAM
    //noinspection GradleDependency
    implementation 'com.stripe:stripe-android:2.0.2' // NOT YET SUPPORTED BY TELEGRAM
    implementation 'com.google.mlkit:language-id:17.0.5'
    implementation 'com.android.billingclient:billing:6.2.0'
    implementation 'org.apache.commons:commons-text:1.10.0'
    implementation 'org.codeberg.qwerty287:prism4j:003cb5e380'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation ('com.google.guava:guava:31.1-jre') {
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
        exclude group: 'org.checkerframework', module: 'checker-compat-qual'
        exclude group: 'org.checkerframework', module: 'checker-qual'
        exclude group: 'com.google.errorprone', module: 'error_prone_annotations'
        exclude group: 'com.google.j2objc', module: 'j2objc-annotations'
        exclude group: 'org.codehaus.mojo', module: 'animal-sniffer-annotations'
    }

    // CameraX Implementation
    def camerax_version = "1.3.0-beta01"
    implementation "androidx.camera:camera-camera2:${camerax_version}"
    implementation "androidx.camera:camera-core:${camerax_version}"
    implementation "androidx.camera:camera-extensions:${camerax_version}"
    implementation "androidx.camera:camera-lifecycle:${camerax_version}"
    implementation "androidx.camera:camera-view:${camerax_version}"
    implementation "androidx.camera:camera-video:${camerax_version}"
    implementation 'androidx.interpolator:interpolator:1.0.0'
    implementation 'androidx.biometric:biometric:1.1.0'

    // GOOGLE PLAY UPDATE CHECKER
    implementation 'com.google.android.play:core:1.10.3'

    // NATIVE LIBS
    implementation 'com.getkeepsafe.relinker:relinker:1.4.5'
    
    implementation 'com.google.android.gms:play-services-mlkit-subject-segmentation:16.0.0-beta1'
    constraints {
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.8.0") {
            because("kotlin-stdlib-jdk7 is now a part of kotlin-stdlib")
        }
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.0") {
            because("kotlin-stdlib-jdk8 is now a part of kotlin-stdlib")
        }
    }

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.4'
}

android {
    namespace 'org.telegram.messenger'

    sourceSets.main.jniLibs.srcDirs = ['./jni/']

    buildFeatures {
        prefab true
    }

    externalNativeBuild {
        cmake {
            path 'jni/CMakeLists.txt'
        }
    }

    buildTypes {
        debug {
            resValue "string", "BuildAppName", "@string/AppNameBeta"
            buildConfigField "boolean", "DEBUG_PRIVATE_VERSION", "true"
            buildConfigField "String", "BUILD_VERSION_STRING", "\"${getVersionPBetaName()}\""
        }

        pbeta {
            resValue "string", "BuildAppName", "@string/AppNameBeta"
            buildConfigField "boolean", "DEBUG_PRIVATE_VERSION", "true"
            buildConfigField "String", "BUILD_VERSION_STRING", "\"${getVersionPBetaName()}\""
        }

        release {
            resValue "string", "BuildAppName", "@string/AppName"
        }
    }

    defaultConfig {
        buildConfigField "String", "GIT_COMMIT_HASH", "\"${getGitHash()}\""
        buildConfigField "Long", "GIT_COMMIT_DATE", "${getGitDate()}L"
        buildConfigField "boolean", "DEBUG_PRIVATE_VERSION", "false"
        buildConfigField "int", "BUILD_VERSION", "${APP_VERSION_CODE}"
        buildConfigField "String", "BUILD_VERSION_STRING", "\"${APP_VERSION_NAME}\""
        buildConfigField "boolean", "BUILD_HOST_IS_WINDOWS", "${isWindows}"

        externalNativeBuild {
            cmake {
                version '3.10.2'
                arguments '-DANDROID_STL=c++_static', '-DANDROID_PLATFORM=android-21'
                var file = getCCacheExecutable()
                if (file.exists()) {
                    println("Using ccache ${file.getAbsolutePath()}")
                    arguments += "-DANDROID_CCACHE=${file.getAbsolutePath()}"
                } else {
                    throw new Exception("ccache not found")
                }
            }
        }
    }
}
